<?xml version="1.0"?>
<popups:Popup xmlns:fx="http://ns.adobe.com/mxml/2009"
              xmlns:popups="ui.popups.*"
              xmlns:s="library://ns.adobe.com/flex/spark"
              skinClass="skins.PanelSkinWithShadow">

    <fx:Script><![CDATA[
        import com.greensock.TweenLite;

        import global.Icons;
        import global.Net;

        import logic.Console;
        import logic.PopupManager;

        import mx.events.CollectionEvent;
        import mx.events.FlexEvent;

        public var popupOnClose:Popup;

        override public function onCreationComplete(event:FlexEvent):void
        {
            super.onCreationComplete(event);
            list.dataProvider = Console.history;
            scrollToBottom();

            list.dataProvider.addEventListener(CollectionEvent.COLLECTION_CHANGE, onHistoryChange);
        }

        private function onHistoryChange(event:CollectionEvent):void
        {
            scrollToBottom();
        }

        private function scrollToBottom():void
        {
            callLater(function ():void
            {
                list.validateNow();
                TweenLite.killTweensOf(list.scroller.verticalScrollBar);
                TweenLite.to(list.scroller.verticalScrollBar, .4, {value: list.scroller.verticalScrollBar.maximum + 100});
            });
        }

        private function onButtonClose(event:MouseEvent):void
        {
            if (popupOnClose)
                PopupManager.open(popupOnClose);
            else
                PopupManager.close();
        }

        private function onButtonSend(event:MouseEvent):void
        {
            sendCommand();
        }

        private function onKeyDown(event:KeyboardEvent):void
        {
            if (event.keyCode == Keyboard.ENTER)
                sendCommand();
        }

        private function sendCommand():void
        {
            var command:String = inputConsole.text;
            if (command.length > 0)
                Net.connection.send("Chat", command);
            inputConsole.text = "";
        }

        override public function dispose():void
        {
            super.dispose();

            list.dataProvider.removeEventListener(CollectionEvent.COLLECTION_CHANGE, onHistoryChange);
        }
        ]]>
    </fx:Script>

    <s:VGroup width="500"
              height="400"
              horizontalAlign="center">
        <s:HGroup width="100%"
                  verticalAlign="middle">
            <s:Label text="Console"/>
            <s:Spacer width="100%"/>
            <s:Button id="buttonClose"
                      label="Close"
                      icon="{Icons.Exit}"
                      click="onButtonClose(event)"/>
        </s:HGroup>

        <s:List id="list"
                width="100%"
                height="100%"
                horizontalScrollPolicy="off"
                borderVisible="false"
                contentBackgroundAlpha="0"
                itemRenderer="ui.renderers.ConsoleItemRenderer"/>
        <s:HGroup width="100%"
                  horizontalAlign="center">
            <s:TextInput id="inputConsole"
                         skinClass="skins.TextInputSkin"
                         width="100%"
                         prompt="Chat"
                         keyDown="onKeyDown(event)"/>
            <s:Button id="buttonSend"
                      icon="{Icons.Enter}"
                      click="onButtonSend(event)"/>
        </s:HGroup>
    </s:VGroup>

</popups:Popup>
